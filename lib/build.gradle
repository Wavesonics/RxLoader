apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

group = 'com.philosophicalhacker.rxloader'
version = '0.1.0'
project.archivesBaseName = 'rxloader'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'RxLoader'
            description 'A tiny data-loading library for android that leverages Android loaders with RxJava'
            url 'https://github.com/kmdupr33/Rxloader'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'com.philosophicalhacker.rxloader'
            artifactId 'rxloader'
            version '0.1.0'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            scm {
                connection 'https://github.com/kmdupr33/Rxloader.git'
                url 'https://github.com/kmdupr33/Rxloader'

            }
            developers {
                developer {
                    name 'Matt Dupree'
                }
            }
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'RxLoader'
        userOrg = System.getenv('BINTRAY_USER')
        name = 'com.philosophicalhacker.rxloader:rxloader'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kmdupr33/RxLoader'
        version {
            name = android.defaultConfig.versionName
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.1.0'
    compile 'io.reactivex:rxjava:1.1.8'
    testCompile 'junit:junit:4.12'
}
